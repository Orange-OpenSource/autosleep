buildscript {
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
        classpath("org.kt3k.gradle.plugin:coveralls-gradle-plugin:$gradleCoverallsVersion")
    }
    repositories {
        mavenLocal()
        jcenter()
    }
}
plugins {
    id "com.github.hierynomus.license" version "0.12.1"
}


subprojects {
    apply plugin: 'java'

    apply from: "$rootProject.projectDir/test.gradle"
    apply from: "$rootProject.projectDir/coverage.gradle"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        jcenter()
        //URL repo to get the desired compiled version
        maven { url "https://jitpack.io" }
        maven { url 'http://repo.spring.io/libs-snapshot' }

    }

    dependencies {
        // Lombok
        compileOnly "org.projectlombok:lombok:$lombokVersion"

        //Groovy templates
        compile "org.codehaus.groovy:groovy-templates:${groovyVersion}"

        //CF java client library and dependencies
        compile("org.cloudfoundry:cloudfoundry-client-spring:$cfClientLibVersion")
        compile("io.projectreactor:reactor-core:$reactorCoreVersion")
        compile "com.google.protobuf:protobuf-java:$protobufVersion"

        //Logs
        compile "org.slf4j:slf4j-api:$slf4jVersion"
        compile "org.slf4j:jcl-over-slf4j:$slf4jVersion"
        // Spring Data
        compile "org.springframework.data:spring-data-jpa:$springDataJpaVersion"
        compile "org.springframework.cloud:spring-cloud-spring-service-connector:$springCloudServiceConnectorVersion"
        // Persistence
        runtime "mysql:mysql-connector-java:$mysqlConnectorVersion"
        runtime "com.h2database:h2:1.4.191"
        compile "commons-dbcp:commons-dbcp:1.4"
        compile "org.hibernate:hibernate-entitymanager:4.3.11.Final"
        compile "org.liquibase:liquibase-core:$liquibaseVersion"

        //Spring boot
        compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
        compile "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"
    }




    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-parameters"
    }



    sourceSets.main.compileClasspath += configurations.compileOnly
    sourceSets.test.compileClasspath += configurations.compileOnly
    sourceSets.test.runtimeClasspath += configurations.compileOnly

}






license {
    header rootProject.file('config/HEADER')
    strictCheck true
    excludes(["**/*.css", "**/boost*", "**/bootbox*", "**/jquery*", "**/banner.txt", "**/*png",
              "**/*eot", "**/*ttf", "**/*woff*", "**/*gif", "**/*jpg", "**/*tpl", "**/*svg", "**/*map",
              "**/*xml", "**/*yaml"])
    mapping {
        java = 'PHP'
    }
}


//no more wrapper task, use gradle wrapper --gradle-version 2.12 to generate wrappre
